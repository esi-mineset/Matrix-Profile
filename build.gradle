buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.nd4j:nd4j-native-platform:1.0.0-beta3"
    compile group: 'com.github.wendykierp', name: 'JTransforms', version: '3.1'
}


// Signing
//apply plugin: 'signing'
//signing {
//    sign configurations.archives
//}


// Deploying
apply plugin: 'maven'

// Add Javadoc JAR and sources JAR to artifact
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// This jar will have just the compiled source for this project
jar {
    baseName = 'matrix-profile'
    classifier = 'stripped'
}
// This jar will have averything including native libs for all platforms.
// Normally the classifier would be "all",
// but unfortunately spark_submit cannot recognize the classifier coordinate so
// the shaded jar has not classifier, while the regular one uses "stripped" to identify it.
shadowJar {
    baseName = 'matrix-profile'
    classifier = ''
}

artifacts {
    archives jar, javadocJar, sourcesJar, shadowJar
}


group = "io.github.ensozos"
archivesBaseName = "matrix-profile"
// Because of https://issues.apache.org/jira/browse/SPARK-20075 spark --packages won't recognize
// the "all" classifier, so, as a work around, change the version name to have "shaded" in it when running the
// uploadShadow task.
//version '0.0.4-shaded-SNAPSHOT'
version '0.0.4-mineset-SNAPSHOT'

uploadShadow {
    repositories {
        mavenDeployer {
            repository(url: "http://esi-components.esi-group.com/artifactory/local-maven-snapshot") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
        }
    }
}

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            //beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            // Destination
            repository(url: "http://esi-components.esi-group.com/artifactory/local-maven-snapshot") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name 'Matrix Profile'
                packaging 'jar'
                description 'A Java library that allows you to use the Matrix Profile concept as a toolkit'
                url 'https://github.com/ensozos/Matrix-Profile'

                scm {
                    connection 'scm:git:git://github.com/ensozos/Matrix-Profile.git'
                    developerConnection 'scm:git:ssh://github.com/ensozos/Matrix-Profile.git'
                    url 'http://github.com/ensozos/Matrix-Proifle/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ensozos'
                        name 'Zosimadis Ilias'
                        email 'zosimadi@csd.auth.gr'
                        organization 'ensozos'
                        organizationUrl 'https://ensozos.github.io/Matrix-Profile/'
                    }
                }
            }
        }
    }
}